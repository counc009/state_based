function copy(dst : path, src : path, src_loc : file_system,
              group: string, owner: string) -> bool {
  if exists fs(src, src_loc) {
    match fs(src, src_loc).fs_type {
      file_type::file(content) => {
        let dst = ends_with_dir(dst) ? cons_path(dst, base_name(src)) : dst;
        fs(dst, file_system::remote).fs_type = file_type::file(content);
        fs(dst, file_system::remote).owner = owner;
        fs(dst, file_system::remote).group = group;
        return true;
      }
      file_type::directory(files) => {
        for f in files {
          assert exists fs(f, src_loc);
          match fs(f, src_loc).fs_type {
            file_type::file(_) => {
              if !copy(cons_path(dst, path_from(src, f)), f, src_loc, group, owner) {
                return false;
              }
            }
            file_type::directory(_) => { return false; }
            file_type::hard(_) => { return false; }
            file_type::link(_) => { return false; }
          }
        }
        return true;
      }
      file_type::hard(_) => { return false; }
      file_type::link(_) => { return false; }
    }
  } else {
    return false;
  }
}

struct copy_result { failed : bool }

module ansible.builtin.copy -> copy_result {
  (dest: path) (src: path | content: string)
  [group: string] [owner: string]
  (force: bool = true)

  if provided src {
    [remote_src : bool = false]

    let loc = remote_src ? file_system::remote : file_system::local;

    if exists fs(src, loc) {
      let group = provided group ? group
                : env().is_root  ? fs(src, loc).group
                                 : env().group;
      let owner = provided owner ? owner
                : env().is_root  ? fs(src, loc).owner
                                 : env().user;

      let do_copy = exists fs(dest, file_system::remote) ? force : true;
      if do_copy {
        if copy(dest, src, loc, group, owner) {
          return copy_result { failed: false };
        } else {
          return copy_result { failed: true };
        }
      } else {
        return copy_result { failed: false };
      }
    } else {
      return copy_result { failed: true };
    }
  } else {
    let do_copy = exists fs(dest, file_system::remote) ? force : true;
    if do_copy {
      # NOTE: The docs are unclear whether the group/owner and updated if we
      # don't perform the copy, need to figure that out
      fs(dest, file_system::remote).fs_type = file_type::file(content);
      fs(dest, file_system::remote).group = provided group ? group : env().group;
      fs(dest, file_system::remote).owner = provided owner ? owner : env().user;
    }

    return copy_result { failed: false };
  }
}
