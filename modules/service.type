struct service_result { failed: bool }

enum service_state { reloaded, restarted, started, stopped }

element e_service(string)
attribute start_on_boot(bool)
attribute running(bool)

attribute started(int)
attribute loaded(int)

module ansible.builtin.service aka service -> service_result {
  (name: string)
  [state: service_state]

  if provided state {
    [enabled: bool]
    if provided enabled {
      e_service(name).start_on_boot = enabled;
    }

    match state {
      # TODO: We probably need some way to indicate restarting/reloading of the
      # service. My inclination is some sort of time counter in the environment
      # that we can use to indicate actions like that
      service_state::reloaded => {
        let time = get_time();
        if exists e_service(name) {} else { e_service(name).running = false; }
        if !e_service(name).running {
          e_service(name).running = true;
          e_service(name).started = time;
        }
        e_service(name).loaded = time;
      }
      service_state::restarted => {
        let time = get_time();
        e_service(name).running = true;
        e_service(name).started = time;
        e_service(name).loaded = time;
      }
      service_state::started => {
        if exists e_service(name) {} else { e_service(name).running = false; }
        if !e_service(name).running {
          let time = get_time();
          e_service(name).running = true;
          e_service(name).started = time;
          e_service(name).loaded = time;
        }
      }
      service_state::stopped => { e_service(name).running = false; }
    }
  } else {
    (enabled: bool)
    e_service(name).start_on_boot = enabled;
  }

  return service_result { failed: false };
}
